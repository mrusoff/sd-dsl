grammar fr.chaunier.xtext.mapping.MapDsl with org.eclipse.xtext.common.Terminals

generate mapDsl "http://www.chaunier.fr/xtext/mapping/MapDsl"

import  "platform:/resource/fr.chaunier.xtext.omc/src-gen/fr/chaunier/xtext/omc/OmcDsl.ecore" as mm 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore 

ModelMap :
	('[' spec =  FileSpecModel ']')? 
	'model' name=QualifiedName (documentation=STRING)?
	'Entity source' entityFrom=[mm::Entity|QualifiedName] (sourcePath=STRING)? 'Entity target' entityTo=[mm::Entity|QualifiedName] (targetPath=STRING)? 
	imports+=Import*
	actions+=Action*
	embeddedModelMap+=EmbeddedModelMap*
;

EmbeddedModelMap :	
	'embedded model' name=QualifiedName (documentation=STRING)?
	('Entity source' entityFrom=[mm::Entity|QualifiedName])? ('Entity target' entityTo=[mm::Entity|QualifiedName])?  
	actions+=Action*
;

Import:
	'import' importedNamespace=QualifiedNameWithWildCard;

QualifiedNameWithWildCard:
	QualifiedName '.*'?;

QualifiedName:
	ID ('.' ID)*;


Action : 
	FunctionDef | RuleDef | MapGroup 
; 

MapGroup : 'map' name=ID (description=STRING )? '{' 
	(featurePath+=FeatureAll)*	
'}'
;


FeatureAll :
	FeatureSet | FeaturePath | EntityFeatureCall
;

FeatureSet :
	'set' in=InOut path=Path '=' value = TerminalExpression (description=STRING )?
;

EntityFeatureCall :
	'call map module' from=FeaturePathFrom  to=FeaturePathTo (call=[EmbeddedModelMap|QualifiedName])?  ('rule' rule=[RuleDef] ('args' args=STRING)?)? (description=STRING )?
;

FeaturePath :
	from=FeaturePathFrom to=FeaturePathTo (forEach?='for each')?  ('rule' rule=[RuleDef])?  (description=STRING )? 

;

FeaturePathFrom :
	'from' path=Path 
;

FeaturePathTo :
	'to' path=Path
;

Path:
	attribute=[mm::Attribute] ({PathParam.target=current} '.' refAttribute=[mm::Attribute])*  
	;

RuleDef:
	'define rule' name=ID ('operation' operation=STRING)? (description=STRING )?  
;

enum InOut:
	in="In" | out="Out" ;

FunctionDef: 
	'fonction' name=ID ('operation' operation=STRING)? (description=STRING )? '{'
		params+=FunctionParam* 
	'}'
;

FunctionParam: 
	in=InOut path=Path
;

TerminalExpression:
   int = IntVar |
   number = NumberVar |
   null = NullVar |
   bool = BoolVar |
   string = StringVar //| 
//   enumm = EnumVar  
;

IntVar : value = BIGINT;
NumberVar : value = NUMBER;
StringVar : value = STRING;
NullVar : value = 'null';
BoolVar : value = 'true' | 'false';

terminal BIGINT returns ecore::EBigInteger:
	('0'..'9')*;

terminal NUMBER returns ecore::EBigDecimal:
	('0'..'9')* ('.' ('0'..'9')+)?;
	
terminal DATE returns ecore::EDate : ('0'..'1') '0'..'9' '.' '0'..'3' '0'..'9' '.'  '0'..'9' '0'..'9' '0'..'9' '0'..'9';

terminal INT returns ecore::EInt:
	'this one has been deactivated';

FileSpecModel:
	specLines += SpecLine*  
;
	
SpecLine :
	SpecInfo | SpecVersion
;

SpecInfo : 
	'info' title=STRING label=STRING   	 
;

SpecVersion : 
	'version' version=STRING comment=STRING   	 
;